// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   
   // Mux -> Not -> Mux: turns x and y into processed version
   Mux16 (a = x, b[0..15] = false, sel = zx, out = x1);
   Not16 (in = x1, out = nx1);
   Mux16 (a = x1, b = nx1, sel = nx, out = x2);
   Mux16 (a = y, b[0..15] = false, sel = zy, out = y1);
   Not16 (in = y1, out = ny1);
   Mux16 (a = y1, b = ny1, sel = ny, out = y2);
   
   // Choose between add and and,then choose between to negate or not
   And16 (a = x2, b = y2, out = xandy);
   Add16 (a = x2, b = y2, out = xplusy);
   Mux16 (a = xandy, b = xplusy, sel = f, out = outtemp);
   Not16 (in = outtemp, out = notouttemp);
   Mux16 (a = outtemp, b = notouttemp, sel = no, out = out);

   // Make a copy of out, name it out1, in order to continue to calculate other outputs
   Mux16 (a = outtemp, b = notouttemp, sel = no, out = out1);
   
   // Take out the most significant bit, if it is 1, then ng is 1
   Not16 (in = out1, out[15] = t);
   Not (in = t, out = ng);
   
   // Negate and add 1 to calculate negative output, then take the most significant bit
   Not16 (in = out1, out = notout1);
   Inc16 (in = notout1, out[15] = t1);
   
   // Only when the previous most significant bit and most significant bit after make it nagetive are both 0 would zr be 1 
   Not (in = t1, out = nott1);
   And (a = t, b = nott1, out = zr);
}